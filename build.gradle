plugins {
    id 'java'
    id 'maven'
    id 'eclipse'
    id 'com.bmuschko.nexus' version '2.1.1'
    id 'us.kirchmeier.capsule' version '0.10.0'
}

//general & artifact properties
group = 'org.fiware.kiara'
version = '0.1.0'
description = "KIARA Advanced Middleware kiaragen tool"

sourceCompatibility = 1.7
targetCompatibility = 1.7

// fix bootstrap class path not set, when compile to different java version
tasks.withType(JavaCompile) {
    options.bootClasspath = "$System.env.JAVA_HOME/jre/lib/rt.jar"
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile group: 'org.fiware.kiara', name: 'kiaraparser', version: '0.1.0'
    compile group: 'org.antlr', name: 'antlr4', version:'4.2.2'
    compile group: 'org.antlr', name: 'stringtemplate', version:'3.2'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.2'
    testCompile group: 'junit', name: 'junit', version:'4.10'
}

test {
  testLogging {
	events 'started', 'passed'
  }
}

task capsule (type:FatCapsule,
              group: 'build',
              description: "Build executable JAR using capsule") {
    applicationClass 'org.fiware.kiara.generator.Kiaragen'
    capsuleManifest.applicationName = project.group+"."+project.name
    capsuleManifest.applicationVersion = project.version
    capsuleManifest.minJavaVersion = '1.7.0'
    //capsuleManifest.extractCapsule = false  // false in case of ThinCapsule
    capsuleManifest.repositories << 'local'  // remove for production
    capsuleManifest.repositories << 'central'
    reallyExecutable // makes jar executable on unix systems
}

artifacts {
    archives capsule
}

install.dependsOn capsule


modifyPom {
    project {
        name project.description
        artifactId project.name
        description project.description
        url 'https://github.com/FIWARE-Middleware'
        inceptionYear '2014'
        
        scm {
          connection 'scm:git:https://github.com/FIWARE-Middleware/kiaragen.git'
          developerConnection 'scm:git:https://github.com/FIWARE-Middleware/kiaragen.git'
          url 'https://github.com/FIWARE-Middleware/kiaragen.git'
        }

        licenses {
          license {
            name 'GNU Lesser General Public License, Version 3.0'
            url 'https://www.gnu.org/licenses/lgpl.html'
          }
        }

        developers {
          developer {
            id 'richiprosima'
            name 'Riccardo Gonzalez'
            email 'RicardoGonzalez@eprosima.com'
          }
          developer {
            id 'RafaelLaraAlvarez'
            name 'Rafael Lara Alvarez'
            email 'RafaelLara@eprosima.com'
          }
          developer {
            id 'dmrub'
            name 'Dmitri Rubinstein'
            email 'Dmitri.Rubinstein@dfki.de'
          }
          developer {
            id 'mach'
            name 'Christof Marti'
            email 'mach@zhaw.ch'
          }
        }
    }
}

extraArchive {
    sources = true
    javadoc = true
    tests = false
}


nexus {
    sign = true
    // To upload to OSSRH comment/remove the following urls
    //repositoryUrl = "file:$buildDir/repos/"
    //snapshotRepositoryUrl = "file:$buildDir/repos/"
}
